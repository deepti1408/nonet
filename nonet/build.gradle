apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'com.getkeepsafe.dexcount'

// Manifest version information major.minor.patch.hotfix
def versionMajor = 3  // Must be in [0, 99]
def versionMinor = 0  // Must be in [0, 99]
def versionPatch = 0  // Must be in [0, 99]
def versionHotfix = 0 // Must be in [0, 9]

project.group = "com.keiferstone"
project.version = "${versionMajor}.${versionMinor}.${versionPatch}-alpha07"

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode versionMajor * 100000 + versionMinor * 1000 + versionPatch * 10 + versionHotfix
        versionName project.version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        releaseConfig {
            storeFile file(project.ext.keystorePath)
            storePassword project.ext.keystorePassword
            keyAlias project.ext.keyAlias
            keyPassword project.ext.keyPassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.releaseConfig
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'com.squareup.okhttp3:okhttp:3.12.0'

    // Instrumentation tests
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Unit tests
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.0.2"
}

publish {
    userOrg = 'keiferstone'
    groupId = project.group
    artifactId = 'nonet'
    publishVersion = project.version
    desc = 'An Android library for monitoring network connectivity'
    website = 'https://github.com/keiferstone/nonet'
}