apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"

Properties localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

// Manifest version information major.minor.patch.hotfix
def versionMajor = 1  // Must be in [0, 99]
def versionMinor = 0  // Must be in [0, 99]
def versionPatch = 6  // Must be in [0, 99]
def versionHotfix = 0 // Must be in [0, 9]

project.group = "com.keiferstone"
project.version = "${versionMajor}.${versionMinor}.${versionPatch}"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25
        versionCode versionMajor * 100000 + versionMinor * 1000 + versionPatch * 10 + versionHotfix
        versionName project.version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        releaseConfig {
            storeFile file(localProperties.getProperty("store.file"))
            storePassword localProperties.getProperty("store.password")
            keyAlias localProperties.getProperty("key.alias")
            keyPassword localProperties.getProperty("key.password")
        }
    }

    buildTypes {
        release {
            signingConfig  signingConfigs.releaseConfig
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // Instrumentation tests
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Unit tests
    testCompile 'junit:junit:4.12'

    // Android support libraries
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'

    // Square libraries
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
}

def siteUrl = 'https://github.com/keiferstone/nonet'        // Homepage URL of the library
def gitUrl = 'https://github.com/keiferstone/nonet.git'     // Git repository URL

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                name project.group + ":nonet"
                description = 'An Android library for monitoring network connectivity'
                url siteUrl

                groupId = project.group
                artifactId = "nonet"
                version = project.version

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'keiferstone'
                        name 'Keifer Stone'
                        email 'keiferstone@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompile.classpath.files
    })
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = localProperties.getProperty("bintray.user")
    key = localProperties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = project.group + ":nonet"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            gpg {
                sign = true
                passphrase = localProperties.getProperty("bintray.gpg.password")
            }
            name = project.version
            desc = 'NoNet Android Library ' + project.version
            released  = new Date()
            vcsTag = project.version
        }
    }
}